# Вы можете расположить сценарий своей игры в этом файле.

# Определение персонажей игры.
define AIHome = Character('Умный дом', color = "#1919b6", image = 'robot')
define gg = Character('Вы', color = "#00fff7")
define artem = Character('Артем', color = "#aefe00", image = 'artem')
define alice = Character('Алиса', color = "#aa1393",image = 'alice')
define pasha = Character('Паша', color = "#fff700",image = 'pasha')
define admin = Character('Администратор', color = "#10c7d0", image = 'receptionist')
define adam = Character('Адам Качинский', color = "#10c7d0", image = 'adam')
define marcin = Character('Марцин Блаха', color = "#10c7d0", image = 'marcin')
define mihal = Character('Михал', color = "#10c7d0", image = 'mihal')
define marcey = Character('Марцей', color = "#10c7d0", image = 'marcey')
define showman = Character('Ведущая', color = "#10c7d0", image = 'receptionist')
define vanya = Character('Ваня', color = "#10c7d0", image = 'vanya')
$ _dismiss_pause = False






define phone_ = Character('Телефон', color = "#a4bda7")
define audio.phone_call = "sounds/rington.mp3"
define audio.bg1 = "music/bg1.ogg"

define audio.bg2 = "music/bg2.ogg"
define audio.sad = "music/sad.ogg"
define audio.happy = "music/happyEnd.mp3"

define audio.travel = "music/travelMusic.mp3"



define audio.aplodisment = "sounds/aplodisment.mp3"
define score = 0
define add_score=0
init python:
    
    onn = ImageDissolve("eye.png", 0.9, 20, reverse=False) 
    off = ImageDissolve("eye.png", 0.7, 10, reverse=True) 
label start:
    image animated = Movie(play="wait.ogv", pos=(0,0), anchor=(0, 0))
    scene animated
    with Pause(1)

    $ name_ = renpy.input("Введите ваше имя", length=11, default = "Максим", allow="йцукенгшщзхъфывапролджэячсмитьбюЙЦУКЕНГШЩЗХЪФЫВАПРОЛДЖЭЯЧСМИТЬБЮ").strip()
    if name_ == "":
        $ name_ = "Максим"
    scene black with dissolve
    "{cps=10}Начинаем погружение,  [name_]{/cps}"
    jump morning
label morning:
    play music bg2
    scene home with onn 
    pause 0.2
    scene black with off
    pause 0.2
    scene home with onn
    pause 0.2
    #сделать миниатюру умного дома
    AIHome "Доброе утро [name_], ваш сегодняшний сон составил всего лишь 4 часа. Не хотел вас тревожить, но вы просили разбудить вас в 6:00, сказали что это очень важно."
    AIHome "Сегодня 4 ноября 2106 года, температура в Москве -9 гардусов, к обеду потеплеет до -2, но лучше одевайтесь потеплее."
    menu:
        "Вы чувствуете себя очень усталым, и готовы пожертвовать всем ради нескольких часов сна"
        "Спать дальше":
            call sleep from _call_sleep
        "Встать, вопреки всему":
            call wake_up from _call_wake_up
    
    jump phone
    return

label sleep:
    gg "Думаю, ничего страшного не произойдет, если поспать еще пару часов"
    play sound phone_call
    "Только вы начали погружаться в сон, как зазвонил телефон"
    return

label wake_up:
    gg "Черт, почему все важные проекты ставят в такую рань?"
    play sound phone_call
    "Стоило вам встать с кровати, как зазвонил телефон"
    return
label phone:
    #Сделать миниатюру Артема
    gg "До начала еще 2 часа, а все уже на нервах..."
    stop sound
    "Вы берете телефон"
    menu:
        artem "Доброе утро [name_], ты ведь не забыл какой сегодня день?"
        "Ой, кажется забыл, можешь напомнить?":
            artem @ annoyed"Хватит дурачится, за еду за тобой через час, отправимся в институт, будь готов"
            "*сбросил трубку*"
            gg "И как в сообществе преданных игровых гиков оказался такой душнила..."
        "Конечно, как о таком забыть?":
            artem "Отлично заеду за тобой через час, поедем в институт"
            "*сбросил трубку*"
    jump road_to_Institute

label road_to_Institute:
    scene black with fade
    "Спустя час"
    scene machine with dissolve
    "Вы садитесь в машину к Артему"
    scene road with dissolve
    gg "Привет, Тема, как спалось?"

    menu:
        artem "Сегодня великий день, наша команда шла к этому долгое время, будущее компьютерных игр зависит только от тебя."
        "Да, да, да. А спалось как?":
            artem @ annoyed "Зря тебе доверили такую ответственную роль, ты слишком не серьезен. Скажи спасибо, что Алиса настояла на твоей кандидатуре."
            gg "И я тебя люблю"
        "Не переживай, я не подведу":
            artem "Надеюсь.."
    gg "Расскажи какой у нас план действий, во сколько начинаем?"
    artem "Вначале Павел, как лидер проекта, скажет речь, потом ты пройдешь небольшой инструктаж и можем начинать."
    jump institute
label institute:
    scene institut with dissolve
    artem "Приехали, нас уже ждут в коворкинге"
    scene institut inside with dissolve
    play music bg1
    "Как только вы входите вас окликает женский голос"
    show alice at right with dissolve
    menu:
        alice "Привет, как спалось?"
        "Нормально..":
            alice "Отлично, кстати, хорошие новости, я буду курировать тебя на протяжении всего твоего \"путешествия\". Так что в любой непонятной ситуации можешь на меня положиться"
        "Я спал 4 часа, зачем начинать так рано?":
            alice "Ой да ладно, для заядлого игромана это небольшая проблема. Кстати, хорошие новости, я буду курировать тебя на протяжении всего твоего \"путешествия\". Так что в любой непонятной ситуации можешь на меня положиться"

    alice "Пошли, Паша сейчас начнет выступать"
    hide alice with dissolve
    scene institut inside_two with fade
    show pasha at left
    pasha "Ребята сегодня великий день для всего игрового сообщества. В этой комнате собрались самые преданные фанаты игровой индустрии."
    pasha "Как вы все знаете, после так называемого игрового кризиса30-ых годов игровая индустрия пришла в упадок. Компании начали делать игры ради выручки, а толковых разработчиков становилось все меньше и меньше."
    pasha "Постепенно игры деградировали и интерес к новым продуктам отпал. Последняя игра вышла 63 года назад."
    pasha "Но даже несмотря на это в мире остаются преданные делу геймеры, они пересматривают старые киберспортивные матчи, играют в игры 70 летней давности."
    pasha "Именно из таких людей состоит наша команда 'Pheonix'. Каждый из нас свято верит в возрождение игровой индустрии и прикладывает для этого все возможные услилия"
    pasha "И сейчас мы как никогда близки к этому. Сегодня [name_] отправится в 2012 год во времена разработки одной из лучших игр человечества, которую я уверен, каждый из вас проходил не один раз -Ведьмак 3."
    pasha "[name_] вместе с командой разработчиков CD project red пройдет путь создания этой культовой игры, чтобы мы могли собрать все необходимые для гейм-девелопера знания. Некогда популярной, но ныне мертвой профессии"
    pasha "Если сегодня все пройдет по плану наша научно-исследовательская команда превратится в студию разработки игр 'Pheonix'"
    pasha "На этом у меня все."
    play sound aplodisment
    pasha "Да прибудет с нами сила."
    alice "Не будем терять времени. Пошли в лабораторию, проведем последний инструктаж и в бой!"
    alice "[name_], за мной"
    stop sound
    jump prepare_for_travel

label prepare_for_travel:
    scene time_machine with fade
    "Вы заходите в комнату, где установлена машина времени 'Phoenix 12'"
    alice "Впечатляет, не правда ли? Ума не приложу, как Паша выпросил разрешение на ее использование у университета. Наши ученые не любят отправлять в прошлое абы-кого."
    menu:
        "У нас влиятельные спонсоры":
            alice "Тут не поспоришь, Паша смог заинтересовать несколько крупных компаний нашим проектом. Они помогают нам с оборудованием и финансами."
            menu:
                alice "Каждая из них надеется получить большую прибыль от наших будущих игр. А ведь это когда-то и погубило индустрию..."
                "Мы не повторим ошибок прошлого.":
                    alice "Для начала давай отправимся в прошлое и научимся делать игры"
                "Много денег - это всегда хорошо":
                    alice @ annoyed"Какой ты циничный, аж противно! В \"Phoenix\" все работают за идею."
        "Паша не последний человек в университет":
            alice  "Павел Борисович работает в университете уже 10 лет, из которых 5 лет он посвятил этому проекту. Упорства ему не занимать, слишком много сил и времени он потратил на эту авантюру, поэтому никакие преграды его не остановят."
            gg "Повезло нам с руководителем"
            alice "Да, он душка!"
    alice "А теперь давай перейдем к инструктажу" 
    alice "Давай. Я уверена, ты все прекрасно знаешь и помнишь. Но, по протоколу, я должна провести этот инструктаж."
    alice "Итак, перед началом работы ты получишь специальный передатчик, в виде гарнитуры, с помощью которой мы сможем поддерживать связь в прошлом"
    alice "Так же возьми документы некого Феликса Вуйчика польского студента, которому CD project red разрешила наблюдать за созданием 3 Ведьмака, с целью обучения."
    alice " Этого человека, можно сказать, создали наши ребята: придумали ему биографию, сделали паспорт, внесли во все возможные реестры в прошлом и договорились о его обучении с CD project-ами."
    alice "Как тебе уже миллион раз говорили, никто в прошлом не должен узнать, кто и откуда ты на самом деле, иначе крышка нашей будущей великой студии."
    alice "Вроде все, если что ты всегда сможешь позвать меня и я отвечу, а в случае чего верну тебя обратно в настоящее."
    alice "Ах ну и конечно, в прошлом твоя задача - узнать как можно больше про три отрасли гейм-девелопинга: дизайн (визуальная состовляющая игры), написание сценария и непосредственно реализация проекта (написание кода)"
    alice "Вопросы?"
    jump questions

label questions:
    menu:
        "Сколько времени я проведу в прошлом?":
            alice "Университет разрешил нам отправить тебя только на один день, так что действовать придется оперативно."
            jump questions
        "Что случится, если кто-то узнает, что я из прошлого?":
            alice "Уфф, не знаю и знать не хочу, уверена, что ничего хорошего"
            jump questions
        "Напомни, а почему именно Ведьмак 3?":
            alice "В этой игре прекрасно все: сюжет, графика, различные механики, дизайн локаций и т. д. Отличный пример для подражания."
            alice "Ну и это любимая игра Паши."
            jump questions
        "Вопросов нет":
            jump end_prepare
label end_prepare:
    alice "Тогда приступим! Садись в кресло."
    alice "Положи руки на подлокотники и закрой глаза."
    alice "Все, хорошего путешествия! Не подведи нас!"
    $ _dismiss_pause = False
    scene black with off
    
    image animated2 = Movie(play="time_lapse.ogv", pos=(0,0), anchor=(0, 0))
    scene animated2
    with Pause(7)
    play music travel
    jump start_travel

label start_travel:
    scene officeout with fade
    gg "Уфф, тошнотворная процедура"
    menu:
        alice "А ты крепкий, обычно людей выворачивает сразу после перемещения. Как слышно?"
        "Могла бы и предупредить":
            alice"Зачем? Ты бы только напрягся лишний раз"
        "Слышно хорошо":
            alice"Отлично"
    alice"Заходи внутрь"
    scene office with fade
    show receptionist at right with easeinleft
    admin "День добрый, молодой человек, вы к кому?"
    gg "Я студент, у меня сегодня назначена небольшая экскурсия"
    menu:
        admin"Имя фамилию, будьте добры"
        "[name_]":
            call name_not_in_base from _call_name_not_in_base
        "Джим Керри":
            call name_not_in_base from _call_name_not_in_base_1
        "Феликс Вуйчик":
            pass
        "*Шепотом позвать Алису*":
            alice @ facepalm "Ты издеваешься? Феликс Вуйчик, не тупи!"
            gg "Феликс Вуйчик"
    admin "Хорошо, ждите здесь, за вами сейчас придут"
    hide receptionist with moveoutright
    jump talk_with_adam
label name_not_in_base:
    admin "В базе вы не записаны"
    alice @ facepalm "Дурак, ты все испортишь! Феликс Вуйчик"
    gg "По документам, я Джордж Якоби, то мой псевдоним"
    "Администратор посмотрела на вас, как на сумасшедшего, но, проверив ваши документы, принялась еще раз проверять базу"
    alice @ facepalm "{cps=10}Придурок.{/cps}"
    return

label talk_with_adam:
    '.{w=1}.{w=1}.'
    show adam at left with easeinright
    adam "Добрый день, Феликс, простите за ожидание, мне очень приятно видеть молодых энтузиастов в нашем офисе, я Адам Кичинский - глава студии CD project red. Надеюсь, сегодняшний экскурс по нашему офису будет полезным для вас."
    menu:
        adam "Перед тем как мы начнем, позвольте вопрос, что привело вас в игровую индустрию?"
        "Желание разбогатеть":
            $add_score = 0
            $score = score + add_score
            #$renpy.notify(f"Количество ваших очков: {score} (+{add_score})")
            adam "Деньги - это хорошо, но на одном желании разбогатеть далеко не уйдешь... Успех в игровой индустрии напрямую зависит от того, насколько ты любишь то, что ты делаешь."
        "Любовь к играм":
            $add_score = 5
            $score = score + add_score
            #$renpy.notify(f"Количество ваших очков: {score} (+{add_score})")
            adam "Да, многие  фанаты игровой культуры приходят работать эту сферу. Но нужно учесть, что процесс разработки игр сильно отличается от прохождения готовых продуктов. Это тяжелый труд, который не всем приходится по вкусу."
        "Желание создать собственную студию, чьи проекты покорят людей по всему миру":
            $add_score = 10
            $score = score + add_score
            #$renpy.notify(f"Количество ваших очков: {score} (+{add_score})")
            adam "Хороший ответ, надеюсь, у вас все получится. Сегодня наша сфера нуждается в толковых специалистах."
    if add_score>0:
        alice "Ты же понимаешь, что в зависимости от твоих действий будет определяться успех нашего проекта?"
    else:
        alice @ annoyed "Ты же понимаешь, что в зависимости от твоих действий будет определяться успех нашего проекта?"

    adam "Я думаю, можно приступать к экскурсии, наша студия сейчас работает над новой игрой по миру Ведьмака."
    adam "Сегодня вы посетите отдел дизайна, гейм-дизайна и разработки. Начнем с наших гейм-дизайнеров. Идите за мной."
    jump game_design

label game_design:
    scene cabinet with fade
    show marcin at right with easeinleft
    marcin "Добро пожаловать в отдел игрового дизайна, я Марцин Блаха, ведущий сценарист компании."
    menu:
        "Приятно познакомиться, я Феликс Вуйчик":
            $add_score = 5
            $score = score + add_score
            #$renpy.notify(f"Количество ваших очков: {score} (+{add_score})")
            pass
        "Упасть на колени и боготворить его":
            $add_score = 0
            $score = score + add_score
            #$renpy.notify(f"Количество ваших очков: {score} (+{add_score})")
            "На вас посмотрели как на сумасшедшего и подняли на ноги"
        "Очень приятно, для меня это огромная честь общаться с человеком, который написал сценарий к культовому Ведьмаку 3.":
            $add_score = 0
            $score = score + add_score
            #$renpy.notify(f"Количество ваших очков: {score} (+{add_score})")
            marcin "Спасибо большое, я польщен. Но вы что-то путаете, я только работаю над ним"
            "Неловкое молчание..."
            alice @ facepalm "Баран"
        "И что? А я Феликс Вуйчик":
            $add_score = 0
            $score = score + add_score
            #$renpy.notify(f"Количество ваших очков: {score} (+{add_score})")
            marcin "Кхм. Приятно познакомиться, Феликс Вуйчик"

    marcin "Давайте начнем экскурсию в мир разработки игр"
    marcin "Работа гейм-дизайнера играет очень важную роль в создании продукта. Мы отвечаем за все ключевые аспекты игры."
    marcin "От нас зависит будет ли игра успешной."
    marcin "Продукт может быть идеально запрограммирован, а модели будут поражать игрока своей красотой, но если за этим великолепием не будет хорошего сюжета, гармоничной атмосферы и продуманного баланса, проект обречен на провал."
    marcin "Дизайнеры и программисты лишь воплощают наши идеи, без нас не может обойтись ни одна игра."
    scene cabinet meele with fade
    marcin "Пойдем к проектору, там я наглядно все покажу и расскажу."
    scene cabinet book
    marcin "Начнем с главного - это конечно же сценарий, у любой игры должен быть хоть какой-нибудь сюжет, определяющий ее цель для игрока. В нашем новом проекте мы взяли за основу сюжета цикл книг Анджея Сапковского \"Ведьмак\"."
    marcin "Моя задача, как сценариста, создать интересную историю для игрока, которая заставит его проникнуться миром  писателя, также я работаю над внутриигровыми диалогами."
    show cabinet main_menu

    marcin "Гейм-дизайнер также продумывает, как будут выглядеть персонажи, локации и предметы в игре, а еще подбирает музыкальное сопровождение, затем передает свое представление дизайнерам, которые пытаются воплотить его задумки."
    marcin "Таким образом мы создаем атмосферу игры, ее настроение."
    scene cabinet meele

    marcin "Кстати музыкальное сопровождение очень важная часть игры. Подобрать правильные композиции не всегда бывает легко."
    marcin "Хочешь попробовать себя в роли гейм-дизайнера? Попробуй подобрать музыку к трем сценам из нашей новой игры."
    $game_checker = False
    stop music
    call DragNDrop from _call_DragNDrop
    stop sound
    scene cabinet meele

    #$ renpy.notify(f"Количество ваших очков: {score} (+{add_score})")
    play music travel
    if game_checker:
        marcin"Да у тебя талант парень!"
    else:
        marcin "Не лучший выбор как по мне, ну да все приходит с опытом"
    marcin "Также на нашем отделе лежит ответственность за создание так называемого нарративного дизайна. То есть создание повествования через кассеты, дневники, записки, описание предметов. "
    show cabinet note

    marcin "Также нарративный дизайнер следит за тем, чтобы сюжет и геймплей не шли отдельно друг от друга. Условно говоря, следит, чтобы не было несостыковок. Вот кстати пример записки из игры."
    marcin "В общем и целом, профессия геймдизайнера подразумевает под собой полную разработку идей игры. Гейм-дизайнер обязан обладать творческим мышлением."
    marcin "То есть, если ты чувствуешь в себе недостаток креативности и воображения, то скорее всего, тебе лучше выбрать другую профессию."
    show cabinet hp

    marcin "Но не только воображение важно в нашей профессии, гейм-дизайнер отвечает за концепции механик игры, а также за игровой баланс, то есть чтобы игра не была слишком простой или сложной."
    marcin "Да и вообще, мы напрямую работаем с отделом разработки и дизайна, поэтому мы должны трезво оценивать наши технические возможности, поэтому знания в областях дизайна и программирования для нас лишними не будут."
    marcin "На этом у меня, пожалуй, все."
    scene cabinet
    show marcin at right
    jump questions_marcin
label questions_marcin:
    menu:
        marcin "Готов ответить на твои вопросы, если они есть."
        "Сколько составляет средняя зарплата гейм-дизайнера?":
            marcin "Сложно сказать, все зависит от твоего опыта, заслуг, компании в которой ты работаешь. Но на первых парах, как мне кажется, денег много не заработаешь, пока не сделаешь себе имя. В общем, все в твоих руках!"
        "Что вы можете посоветовать начинающим гейм-дизайнерам?":
            marcin "Тяжелый вопрос, наверное, больше читать, книги развивают воображение, а оно крайне нужно в нашем деле. Тем более работа писателей во многом схожа с нашей."
        "Что вы считаете главным в своей работе?":
            marcin "Ох, так сразу и не скажешь. Цель нашей профессии - создать интересный, наполненный красками игровой мир, который сможет увлечь игроков. Каждый успешный продукт обладает своей фишкой, оригинальной идеей, способной удивить аудиторию."
        "Вопросов нет, спасибо, что уделили время, было очень познавательно.":
            marcin "Всегда рад помочь, тогда все, тебе пора в отдел графического дизайна, удачи тебе в твоих начинаниях!"
            gg "Спасибо еще раз, всего доброго"
            jump design

    jump questions_marcin

label design:
    scene cabinet with fade
    show mihal at right with easeinleft
    mihal "Привет, welcome. Ты тот самый студент, которому назначен небольшой экскурс в мир дизайна? Я Михал Мадей, один из главных дизайнеров компании"
    gg "Здравствуйте, рад знакомству"
    mihal "Ну что же, внимание на экран, я буду все рассказывать на примере нашей новой игры"
    scene cabinet meele with fade
    mihal "Итак, в чем же заключается наша работа? Всё, что игрок видит непосредственно на мониторе -  работа графических дизайнеров."
    scene cabinet beautiful
    mihal "Как понятно из названия, данный отдел занимается графической составляющей игры, а этим являются анимации, спецэффекты, модели героев, текстуры мира, освещение, кат-сцены и прочее."
    scene cabinet meele
    mihal "Теперь подробнее пройдемся по основным составляющим"
    mihal "В первую очередь создаются концепт-арты  персонажей и окружения. На этом этапе работают с 2D."
    mihal "Художнику необходимо вжиться в  образ персонажа и отработать идею быстро и эффективно, представить персонажа, отразив все те его характеристики, которые заложены в техническом задании."
    scene cabinet concept
    mihal "Вот концепт-арты одной из героинь нашей новой игры, Трисс Меригольд и ее итоговая модель"
    scene cabinet model
    mihal "Теперь о Моделях. Здесь работают с 3D. Люди этой профессии занимаются созданием высококачественных моделей персонажей и различных объектов игрового мира"
    mihal "При моделировании необходимо учитывать топологию модели, также нужно понимать, какие возможные коллизии могут происходить при том или ином движении модели. После завершения процесса создания все наработки модельера переходят к аниматору."
    scene cabinet meele
    mihal "Аниматоры “оживляют” персонажа. Сначала настраивают скелет персонажа, а затем придают необходимые движения посредством анимационных ключей, которые расставляются при том или ином нажатии кнопки. Аниматору не помешают базовые знания анатомии."
    mihal "Помимо персонажей аниматоры занимаются созданием динамичного окружения: благодаря им глаз игрока радуют травы, колыхающиеся на ветру, горящие в подземельях факела, дождь и другие нестатические явления."
    scene cabinet texture
    mihal "Также в дизайне есть отдельная команда, занимающаяся текстурами. Данная команда работает с различными поверхностями (лёд, вода, камень, металл)."
    mihal "Чем детальнее текстуры, тем лучше, но не стоит и забывать про производительность, которая может страдать от неоптимизированных объектов и анимаций."
    scene cabinet light
    mihal "Также в команде есть люди, отвечающие  за освещение во всех локациях и сценах игры, они настраивают блики, яркость и интенсивность света каждой локации."
    mihal "Основная задача - найти баланс между светом и цветом и расставить необходимые акценты по ходу развития сюжета игры. Знания программирования здесь будут жирным плюсом."
    scene cabinet interface
    mihal "Ну и напоследок расскажу о UX и UI дизайне. Это создание пользовательского интерфейса. Определение темы, цветов, формы кнопок. Также сюда входит подбор шрифтов и определение макета интерфейса."
    mihal "UX и UI дизайнеры стремятся создать простой, удобный и стильный интерфейс. Гармоничный интерфейс всегда радует глаз."
    scene cabinet 
    show mihal at right
    mihal "В общем и целом, всё что игрок видит на мониторе -  наша работа. Как понятно из названия наш отдел занимается графической составляющей игры, а этим являются анимации, спецэффекты, модели героев, текстуры мира, освещение, кат-сцены и прочее. "
    mihal "Чтобы работать в каком-либо ответвлении графического дизайна, помимо хорошего воображения неплохо иметь специфичные знания этого ответвления - теория цвета и свет, понимание ракурсов съемки, знание анатомии. Профессия достаточно креативная и интересная."
    mihal "Вот собственно говоря и все"
    jump questions_mihal
label questions_mihal:
    menu:
        mihal "Есть какие-нибудь вопросы?"
        "Дизайнер - прибыльная профессия?":
            mihal "Все зависит от твоего профессионализма, опыта и заслуг, как впрочем и везде. Я не жалуюсь"
        "Что, по вашему, главное в профессии графического дизайнера?":
            mihal "Любить свое дело, графический дизайнер - художник, задача которого воплотить в жизнь создавшийся в голове образ. Нельзя вымучивать из себя рисунок или модель, нужно, чтобы работа доставляла удовольствие, тогда успех не заставит себя ждать"
        "Можете  дать совет начинающим дизайнерам?":
            mihal "Конечно, рисуйте больше, пробуйте, экспериментируйте. делитесь своими работами на форумах, чтобы услышать критику. Чем больше у вас опыта, тем лучше"
        "Вопросов нет, спасибо за такую познавательную экскурсию":
            mihal "Всегда пожалуйста, рад бы пообщаться"
            "*Уходит*"
            alice "Отлично, остался последний рывок!"
            jump game_development
    jump questions_mihal

label game_development:
    scene door with fade
    with Pause(2)
    scene office2
    show marcey at right with easeinleft
    gg "День добрый, есть кто?"
    marcey "О, да, привет! Ты наверное тот студент, о котором меня предупреждали. Я Мацей Синило, можно сказать, главный прогер компании, работал над вторым и первым Ведьмаком"
    gg "Приятно познакомиться, Марцей!"
    marcey "Что же работы много, так что не будем затягивать, начнем нашу экскурсию"
    marcey "Садись рядом, покажу тебе наши наработки"
    scene computer office
    marcey "Чем вообще занимается разработчик при создании игры?"
    marcey "Когда готов сюжет и визуальная составляющая игры, настаёт время кода."
    scene computer office code
    marcey "Программисты в игровой индустрии по своей сути занимаются внедрением всех наработок художников, геймдизайнеров, музыкантов."
    marcey "Для работы в этом отделе необходимы знания языков, которые используются при создании игры. Сразу скажу, что без знаний по математике и алгоритмике в сферу разработки лучше не лезть"
    scene computer office engine
    marcey "В первую очередь выбирается или создается с нуля игровой движок, на котором будет работать проект. Игровой движок - базовое программное обеспечение компьютерной игры."
    marcey "Говоря грубо, игровой движок — это какой-то инструментарий, упрощающий разработку игры и дающий ей кроссплатформенность. Движок определяет физику игры, анимации героев, оптимизацию, от части графику."
    marcey "Сейчас для создания третьего  Ведьмака мы разработали собственный игровой движок - REDengine 3. Кстати, написан он на C++"
    scene computer office intellect
    marcey "Затем прописывается искусственный интеллект игры. Программист ИИ может программировать систему поиска пути, стратегии и тактики противника."
    marcey "Это особенно важно в стратегических играх. Данная сфера довольна сложна, она тесна связана не только с программированием, но и с математикой и теорией алгоритмов."
    menu:
        marcey "Кстати, у нас есть небольшой тренажер для разработчиков в сфере искусственного интеллекта, хочешь попробовать свои силы?"
        "С удовольствием":
            pass
        "Не рискну":
            alice annoyed "Ты что обалдел? Пробуй давай, нельзя упускать такую возможность"
            gg "Хотя, когда еще выпадет такая возможность? Надо пробовать"
    call RPS_game from _call_RPS_game
    scene computer office sound_
    marcey "Еще разработчики занимаются синхронизацией звука с персонажами, действиями, событиями. (выстрел из пистолета, удар грома, музыкальный фон). Также мы распределяем звуки по коллекциям, изменяем звуковую параметрию, занимаемся сведением звуков."
    marcey "Я же являюсь ведущим программистом. Я ответственен за всю кодовую составляющую игры."
    marcey "Моя работа заключается в том, чтобы убедиться, что различные подмодули игры реализованы должным образом, я слежу за развитием событий с точки зрения программирования."
    marcey "Чтобы стать таким специалистом, нужно несколько лет проработать в игровой индустрии."
    marcey "Несмотря на свою должность, ведущий программист имеет меньше времени на написание кода, так как в его обязанности входит проведение собраний и контроль деятельности других программистов."
    scene office2
    show marcey at right
    marcey "Наша экскурсия подошла к концу. Могу ответить на твои вопросы, если они есть, но не долго, время - деньги"
    jump questions_marcey

label questions_marcey:
    menu:
        marcey "Какие у тебя есть вопросы?"
        "Может, у вас есть какой-нибудь совет для начинающих разработчиков?":
            marcey "Учиться. Смотреть обучающие видео, записываться на курсы, поступить в университет на программиста. Мне кажется, это единственный путь к успеху"
        "Сколько в среднем зарабатывает разработчик игр за месяц?":
            marcey "Тяжело сказать, ну, в среднем около 1800 долларов"
            gg "*Про себя* Примерно 100000 рублей, неплохо, неплохо..."
        "Не буду отвлекать, спасибо огромное, что уделили время, всего хорошего!":
            marcey "Успехов"
            jump end_travel
    jump questions_marcey

label end_travel:
    scene officeout with fade
    alice "Молодец, задача выполнена. Пора назад в будущее, ты готов?"
    gg "Поехали"
    alice "Перемещение через 3... {w=1}2... {w=1}1..."
    play music bg1

    image animated2 = Movie(play="time_lapse.ogv", pos=(0,0), anchor=(0, 0))
    scene animated2
    with Pause(7)
    jump back_to_the_future
 
label back_to_the_future:
    scene time_machine with fade
    gg "Бе.. Отвратительное чувство"
    alice "Все почти позади, осталось только записать полученный материал"
    gg "Опять работа... Что надо делать?"
    alice "Пошли за стол"
    scene pc with fade
    alice "Теперь запиши, что ты узнал на этот компьютер, пароль на листочке. Я пока пойду заполню рапорт"
    scene pc meele
    call screen safe_code
    "Войди в систему"
    scene pc meele
label test:
    'Отлично, ты зашел'
    menu:
        "За разработку идей, фишек, механик игры отвечает..."
        "Дизайнер":
            pass
        "Программист":
            pass
        "Гейм-дизайнер":
            $score = score + 5
        "Ресепшеонист":
            pass
    menu:
        "Какие качества необходимы для работы гейм-дизайнером?"
        "Сила воли, пацифизм":
            pass
        "Креативность, грамотность":
            $score = score + 5
        "Юмор, манерность":
            pass
        "Коллаборационизм, коммунизм":
            pass
    menu:
        "За какие аспекты игры отвечает графический дизайнер?"
        "Создание текстур, прорисовка персонажей":
            $score = score + 5
        "Тестирование, исправление багов":
            pass
        "Написание сюжета, создание баланса":
            pass
        "Музыку пишет, типо рэпер":
            pass
    menu:
        "Какими навыками/знаниями должен обладать разработчик (программист)?"
        "Азы математики, знание языков программирования":
            $score = score + 5
        "Высшее образование, числа Фибоначчи":
            pass
        "Слепая печать, логика":
            pass
        "Должен знать аккорды песни 'Моя оборона' и число Pi до 12 цифры после запятой":
            pass
    menu:
        "Кто занимается сведением музыки в игре?"
        "Гейм-дизайнер":
            pass
        "Разработчик":
            $score = score + 5
        "Дизайнер":
            pass
        "Композитор":
            pass
    menu:
        "Кто отвечает за освещение в игре?"
        "Гейм-дизайнер":
            pass
        "Разработчик":
            $score = score + 5
        "Дизайнер":
            pass
        "Электрик":
            pass
    menu:
        "Выберете правильную цепочку"
        "Анимация персонажа => Концепт арт => Модель персонажа":
            pass
        "Модель персонажа => Концепт арт => Анимация персонажа":
            pass
        "Концепт арт => Анимация персонажа => Модель персонажа":
            pass
        "Концепт арт => Модель персонажа => Анимация персонажа":
            $score = score + 5

    menu:
        "Что такое игровой движок?"
        "Инструмент для создания 3D моделей":
            pass
        "Ну типо как у машины, шоб на экране что-то двигалось":
            pass
        "Инструментарий, упрощающий разработку игры и дающий ей кроссплатформенность":
            $score = score + 5
        "Язык программирования":
            pass
    alice "О, ты уже закончил! Отлично, теперь все в наших руках, можно приступать к созданию игры"
    $score = score + 60
    scene black with fade
    "Спустя 8 лет..."
    
    #menu:
    #    "Выберите концовку (в проде такого не будет)"
    #    "Хорошая":
    #        $score = score + 60
    #    "Плохая":
    #        $score = score - 60
    

    if score > 39:
        jump good_end
    else:
        jump bad_end

label good_end:
    play music happy
    scene beforeshow with fade
    alice "Включай стрим скорее, сейчас начнется"
    alice "Подумать только Паша гость Late Night Show, это победа!"
    gg "Да включаю я, включаю"
    scene conference
    show pasha at left with fade
    show receptionist at right with fade
    showman " Сегодня у нас в программе глава новой студии игр 'Pheonix', чья новая игра покорила сердца миллионов человек по всему миру и перевернула современную индустрию развлечений"
    showman "Сейчас Павел расскажет, как команде студентов удалось возродить казалось бы мертвый жанр видео-игр."
    play sound aplodisment
    showman "Добрый вечер Павел, ваша игра произвела настоящий фурор, сколько копий вы продали?"
    stop sound
    pasha "Добрый вечер, если не ошибаюсь наш тираж превысил 200 млн копий"
    showman "200 млн!? Это невероятный результат, скажите как вам это удалось?"
    pasha "Наша команда проделала долгий и тяжелый путь, при создании нашей игры мы изучали успешные проекты прошлого, пытались создать свой оригинальный проект, переняв опыт предшественников"
    pasha "Мне кажется нам это удалось."
    showman "Безусловно Павел, безусловно. После вашего успеха многие люди хотят повторить ваш успех, что вы можете им посоветовать?"
    pasha "Ну это зависит какую роль при разработки игры вы хотите играть, так, например, если вы хотите стать программистом в команде разработчиков, наверное вам стоит получить IT образование"
    pasha "Допустим, почти все наши разработчики - учились на специальности Программная инженерия в Уральском федеральном университете, так что выбирайте вуз, сдавайте экзамены, поступайте и обучайтесь"
    pasha "Кто знает может следующий прорыв в игровой индустрии совершите именно вы!"
    jump endGame


label bad_end:
    play music sad
    scene macdac with fade
    vanya "[name_], две картошки и Биг Мак"
    gg "Будет сделано..."
    scene macdac outside with fade
    "Через два часа..." 
    vanya "Еще один день на работе мечты, а ведь я международник по образованию, УГИ закончил..."
    vanya "Кстати, а ты на кого учился?"
    gg "На программиста"
    vanya "Реально? Как ты дошел до фастфуда, программисты ведь востребованная профессия?"
    gg "Мы с командой разрабатывали проект, пытались выпустить первую за долгое время успешную игру."
    gg "Все вроде шло хорошо, у нас были спонсоры, энтузиазм, команда, но опыта в этой сфере у нас не хватило, проект буквально разваливался. Работники разбегались кто-куда, спонсоры прекратили финансирование и вот я здесь"
    gg "Я играл довольно таки важную роль в команде, так что во многом это мой провал."
    gg "Если бы я мог вернуться в прошлое и попробовать что-то изменить... Но увы, имеем, что имеем"
    vanya "Грустная история, ладно пора домой, спокойной ночи"
    gg "Спокойной.."
    jump endGame

label endGame:
    scene end1
    with Pause(2)
    scene end2
    with Pause(2)
    scene black with fade
    with Pause(2)

